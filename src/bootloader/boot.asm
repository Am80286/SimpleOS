;    This file is part of SimpleOS.
;
;    SimpleOS is free software: you can redistribute it and/or modify it under the terms of the 
;    GNU General Public License as published by the Free Software Foundation, either version 3 
;    of the License, or (at your option) any later version.
;
;    SimpleOS is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
;    without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
;    See the GNU General Public License for more details.

;    You should have received a copy of the GNU General Public License along with SimpleOS. 
;    If not, see <https://www.gnu.org/licenses/>. 

[org 0x7c00]
[bits 16]

jmp short BOOT_START
nop

;These values get replaced by one generated by mkfs.fat during image creation process
BS_OEMName                  db "MSWIN4.1"
BPB_BytesPerSec             dw 512
BPB_SecPerClus              db 16
BPB_RsvdSecCnt              dw 16
BPB_FatNum                  db 2
BPB_RootEntCnt              dw 512
BPB_TotalSecCnt             dw 0
BPB_Media_Desc_Type         db 0xf8
BPB_SecPerFat               dw 256
BPB_SecPerTrack             dw 63
BPB_Heads                   dw 32
BPB_HiddenSectors           dd 0
BPB_LargeSecCnt             dd 1048576

EBR_BootDrvNumber           db 99
                            db 0
EBR_Signature               db 0x29
EBR_VolumeId                db 0xFF, 0xFF,0xFF, 0xFF
EBR_VolumeLbl               db "NO NAME "
EBR_SysId                   db "FAT16   "

[CPU 386]
[SECTION .text]
    BOOT_START:
        xor ax, ax
        mov es, ax
        mov ds, ax
        mov ss, ax
        
        mov byte[EBR_BootDrvNumber], dl

        mov sp, 0x7c00
        mov bp, sp

    INIT_BOOT_DISK:
        push es

        mov ah, 0x08
        int 0x13
        jc CRIT_ERROR
        pop es

        inc dh
        mov [BPB_Heads], dh
        and cl, 0x3f
        xor ch, ch
        mov [BPB_SecPerTrack], cx

        mov ax, [BPB_SecPerFat]
        mov bx, [BPB_FatNum]
        mul bx
        add ax, [BPB_RsvdSecCnt]
        push ax
        mov ax, 32
        mov bx, [BPB_RootEntCnt]
        mul bx
        add ax, [BPB_BytesPerSec]
        dec ax
        mov bx, [BPB_BytesPerSec]
        div bx
        pop bx
        add ax, bx
        mov [DATA_START_SEC], ax

    LOAD_ROOT_DIR:
        mov al, [BPB_FatNum]
        xor ah, ah
        mov bx, [BPB_SecPerFat]
        mul bx
        add ax, [BPB_RsvdSecCnt]

        push ax ;Root dir start sector
        
        mov ax, [BPB_RootEntCnt],
        shl ax, 5
        xor dx, dx
        div word[BPB_BytesPerSec]

        test dx, dx
        jz .done 
        inc ax

    .done:
        mov cx, ax ;AX - root dir sector count 
        pop ax
        mov bx, BUFFER
        call READ_LBA

    LOAD_STAGE2:
        mov ax, word[BPB_RootEntCnt]
        mov si, BOOT_DIR
        mov di, BUFFER
        call SEARCH_DIR

        xor dx, dx
        mov ax, word[BPB_BytesPerSec]
        mov bx, BUFFER
        add bx, ax                  ; This offset is needed, because the FAT already occupies the first 512 bytes of the buffer space
        
        call LOAD_DIR_ENTRY
        mov ax, 32                   ; MUST BE PROPERLY CALCULATED INSTEAD OF BEING HARD CODED
        mov si, STAGE2_FILE
        mov di, bx
        call SEARCH_DIR

        mov dx, STAGE2_SEG
        mov bx, STAGE2_OFF
        call LOAD_DIR_ENTRY
        
        mov dl, byte[EBR_BootDrvNumber]
        mov ax, STAGE2_SEG
        mov ds, ax
        jmp STAGE2_SEG:STAGE2_OFF

    SEARCH_DIR: ; DS:SI - pointer to name string, ES:DI - pointer to directory, AX - number of entries to check
        pusha
        xor bx, bx

    .loop:
        mov cx, 11
        push si
        push di
        repe cmpsb
        pop di
        pop si

        je .found
        inc bx
        add di, 32
        cmp bx, ax
        jl .loop

        jmp CRIT_ERROR
        
    .found: ;directory entry pointer is in DI register
        mov ax, [di + 26]
        mov [FILE_CLUSTER], ax

        popa
        ret

;for FAT16
%ifndef FAT12

    LOAD_DIR_ENTRY: ; FILE_CLUSTER - first cluster of a directory entry, DX:BX - load adress
        pusha
        push es
        push dx

        .loop:
        pop ax
        mov es, ax
        push ax

        xor ch, ch
        mov cl, byte[BPB_SecPerClus]
        mov ax, word[FILE_CLUSTER]
        sub ax, 2
        mul cx
        add ax, [DATA_START_SEC]

        call READ_LBA

        mov ax, word[BPB_BytesPerSec]
        xor dh, dh
        mov dl, byte[BPB_SecPerClus]
        mul dx
        add bx, ax
        push bx

        xor dx, dx
        mov ax, word[FILE_CLUSTER]
        shl ax, 1
        mov bx, word[BPB_BytesPerSec]
        div bx
        add ax, word[BPB_RsvdSecCnt]
        mov bx, BUFFER_SEG
        mov es, bx
        xor bx, bx
        mov cx, 1

        call READ_LBA

        mov si, dx
        mov ax, word[es:si]

    .after_next_clus:
        cmp ax, 0xFFF8
        pop bx
        jae .done

        mov word[FILE_CLUSTER], ax
        jmp .loop

    .done:
        pop dx
        pop es
        popa
        ret

%endif

;for FAT12
%ifdef FAT12

    LOAD_DIR_ENTRY: ; FILE_CLUSTER - first clustetr of a directory entry, DX:BX - load adress
        pusha

        xor ch, ch
        mov cl, byte[BPB_SecPerClus]
        mov ax, word[FILE_CLUSTER]
        sub ax, 2
        mul cx
        add ax, [DATA_START_SEC]

        call READ_LBA
        
    .done:
        popa
        ret
%endif

    READ_LBA:
        pusha
        push cx ;CX - amount of sectors to read
        push bx ;BX - buffer
        push ax ;AX - LBA 

        mov ax, word[BPB_Heads]
        mul word[BPB_SecPerTrack]
        mov bx, ax
        pop ax
        xor dx, dx
        div bx ;At this moment AX is Cylinder DX is temp

        push ax

        mov ax, dx
        xor dx, dx
        div word[BPB_SecPerTrack]
        inc dx ;At this moment AX is head DX is sector

        mov cx, dx ; CL - sector
        xor dx, dx
        mov dh, al ; DH - head

        pop bx  
        mov ch, bl ; CH - cylinder

        pop bx
        pop ax
        mov ah, 0x02
        mov dl, [EBR_BootDrvNumber]
        
        int 13h
        jc CRIT_ERROR

    .done:
        popa
        ret

    CRIT_ERROR:
        xor ah, ah
        int 16h
        jmp 0xffff:0

FILE_CLUSTER                dw 0
DATA_START_SEC              dw 0

BOOT_DIR                    db "BOOT       "
STAGE2_FILE                 db "LOADER  BIN"

RETC                        equ 0x0D
ENDL                        equ 0x0A
BUFFER_SEG                  equ 0x07e0

STAGE2_SEG                  equ 0x0800
STAGE2_OFF                  equ 0
STAGE2_SIGNATURE            equ 0xBADF

times 510-($-$$) db 0
dw 0xaa55

BUFFER:

;
;
;      _____            __    ____  ____
;     / __(_)_ _  ___  / /__ / __ \/ __/
;    _\ \/ /  ' \/ _ \/ / -_) /_/ /\ \  
;   /___/_/_/_/_/ .__/_/\__/\____/___/  
;              /_/                      
;
;
;